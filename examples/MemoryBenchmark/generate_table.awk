#!/usr/bin/gawk -f
#
# Usage: generate_table.sh < ${board}.txt
#
# Takes the file generated by collect.sh and generates an ASCII
# table that can be inserted into the README.md.

BEGIN {
  NUM_FEATURES = 14
  labels[0] = "Baseline"
  labels[1] = "LocalDateTime"
  labels[2] = "ZonedDateTime"
  labels[3] = "Manual ZoneManager"
  labels[4] = "Basic TimeZone (1 zone)"
  labels[5] = "Basic TimeZone (2 zones)"
  labels[6] = "Basic ZoneManager (1 zone)"
  labels[7] = "Basic ZoneManager (all)"
  labels[8] = "Extended TimeZone (1 zone)"
  labels[9] = "Extended TimeZone (2 zones)"
  labels[10] = "Extended ZoneManager (1 zone)"
  labels[11] = "Extended ZoneManager (all)"
  labels[12] = "SystemClock"
  labels[13] = "SystemClock+Basic TimeZone"
  labels[14] = "SystemClock+Extended TimeZone"
  record_index = 0
}
{
  u[record_index]["flash"] = $2
  u[record_index]["ram"] = $4
  record_index++
}
END {
  base_flash = u[0]["flash"]
  base_ram = u[0]["ram"]
  for (i = 0; i <= NUM_FEATURES; i++) {
    u[i]["d_flash"] = u[i]["flash"] - base_flash
    u[i]["d_ram"] = u[i]["ram"]- base_ram
  }

  printf("+--------------------------------------------------------------+\n")
  printf("| Functionality                   |  flash/  ram |       delta |\n")
  printf("|---------------------------------+--------------+-------------|\n")
  printf("| %-31s | %6d/%5d | %5d/%5d |\n",
      labels[0], u[0]["flash"], u[0]["ram"], u[0]["d_flash"], u[0]["d_ram"])
  printf("|---------------------------------+--------------+-------------|\n")
  for (i = 1; i <= 11; i++) {
    printf("| %-31s | %6d/%5d | %5d/%5d |\n",
        labels[i], u[i]["flash"], u[i]["ram"], u[i]["d_flash"], u[i]["d_ram"])
  }
  printf("|---------------------------------+--------------+-------------|\n")
  for (i = 12; i <= NUM_FEATURES; i++) {
    printf("| %-31s | %6d/%5d | %5d/%5d |\n",
        labels[i], u[i]["flash"], u[i]["ram"], u[i]["d_flash"], u[i]["d_ram"])
  }
  printf("+--------------------------------------------------------------+\n")
}
